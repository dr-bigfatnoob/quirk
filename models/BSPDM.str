Model BSPDM;
Samples 1000;

Min ExpectedCostOfDisclosures = EV(Cost_of_Disclosures);
Min RiskOfCatastrophicDisclosures = PROBABILITY(Excess_Costs);

Excess_Costs = Cost_of_Disclosures > 10^6;

DM Building_entry_security = not_guarded, guarded;
DM Document_Sharing_Policy = Neutral, Recommend_Email, Recommend_External_Media;

Cost_of_Disclosures =
    (Nbr_HighConfidentialityLeaks * Cost_HighConfidentialityLeak)
    + (Nbr_MediumConfidentialityLeaks * Cost_MediumConfidentialityLeak)
    + (Nbr_LowConfidentialityLeaks * Cost_LowConfidentialityLeak);

Nbr_HighConfidentialityLeaks = Nbr_LeakedDoc * Ratio_HighConfidentialityDocs;
Nbr_MediumConfidentialityLeaks = Nbr_LeakedDoc * Ratio_MediumConfidentialityDocs;
Nbr_LowConfidentialityLeaks = Nbr_LeakedDoc * Ratio_LowConfidentialityDocs;

Ratio_HighConfidentialityDocs = uniform(0.005, 0.02);
Ratio_MediumConfidentialityDocs = uniform(0.04, 0.10);
Ratio_LowConfidentialityDocs = uniform(0.30, 0.60);

Cost_HighConfidentialityLeak = normalCI(0.5*10^6, 5*10^6);
Cost_MediumConfidentialityLeak = normalCI(10^4, 5*10^4);
Cost_LowConfidentialityLeak = normalCI(10^2, 10^4);

Nbr_LeakedDoc = Nbr_DocsLeakedByInsiders + Nbr_DocsStolenByOutsiders;

Nbr_DocsLeakedByInsiders =
       Probability_InsidersLeak *
       Nbr_DocsOnSharedDrive *
       SharedDriveLeakRange;

Probability_InsidersLeak = 10^-3;
SharedDriveLeakRange = triangular(0.10, 0.50, 1.00);

Nbr_DocsStolenByOutsiders = Nbr_OfficeIntrusions* Nbr_DocsStolenPerIntrusion;

Decision Building_entry_security:Nbr_OfficeIntrusions = not_guarded:triangular(0, 3, 6), guarded:triangular(0, 1, 2);

Nbr_DocsStolenPerIntrusion = Nbr_ExternalMediaStolenPerIntrusion * Nbr_DocsOnExternalMedia;

Nbr_ExternalMediaStolenPerIntrusion = Nbr_ExternalMediaInUse * Percentage_MediaStolenPerInstrusion;

Percentage_MediaStolenPerInstrusion = triangular(0, 0.01, 0.1);

Decision Document_Sharing_Policy:Nbr_DocsOnSharedDrive =  Neutral:143, Recommend_Email:44, Recommend_External_Media:91;
Decision Document_Sharing_Policy:Nbr_DocsOnExternalMedia =  Neutral:0, Recommend_Email:0, Recommend_External_Media:52;

Nbr_ExternalMediaInUse = Nbr_DocsOnExternalMedia / Nbr_DocsPerMedia;

Nbr_DocsPerMedia = triangular(0, 5, 10);